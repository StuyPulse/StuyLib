buildscript {
   ext.WPI_VER = "2025.1.1"
}

plugins {
   id "java"

   id "maven-publish"

   id "edu.wpi.first.GradleRIO" version "2025.1.1"
   id "com.diffplug.spotless" version "6.22.0"
   id "com.github.spotbugs" version "5.2.0"
}

group = 'com.stuypulse'

java {
   sourceCompatibility = JavaVersion.VERSION_17
   targetCompatibility = JavaVersion.VERSION_17
}

sourceSets {
   main {
      java {
         srcDir "src"
      }
   }
}

configurations {
   jaxDoclet
}

dependencies { 
   implementation wpi.java.deps.wpilib()
   implementation wpi.java.vendor.java()

   nativeDebug wpi.java.deps.wpilibJniDebug(wpi.platforms.desktop)
   nativeDebug wpi.java.vendor.jniDebug(wpi.platforms.desktop)
   simulationDebug wpi.sim.enableDebug()

   nativeRelease wpi.java.deps.wpilibJniRelease(wpi.platforms.desktop)
   nativeRelease wpi.java.vendor.jniRelease(wpi.platforms.desktop)
   simulationRelease wpi.sim.enableRelease()

   implementation "edu.wpi.first.ntcore:ntcore-java:${WPI_VER}"
   implementation "edu.wpi.first.wpiutil:wpiutil-java:${WPI_VER}"
   runtimeOnly "edu.wpi.first.ntcore:ntcore-jni:${WPI_VER}:windowsx86"
   runtimeOnly "edu.wpi.first.ntcore:ntcore-jni:${WPI_VER}:windowsx86-64"
   runtimeOnly "edu.wpi.first.ntcore:ntcore-jni:${WPI_VER}:linuxx86-64"
   runtimeOnly "edu.wpi.first.ntcore:ntcore-jni:${WPI_VER}:osxx86-64"

   implementation group: 'org.knowm.xchart', name: 'xchart', version: '3.2.2'

   testImplementation 'junit:junit:4.12'
}

publishing {
   publications {
      maven(MavenPublication) {
         groupId = 'com.stuypulse.stuylib'
         artifactId = 'library'
         version = "${version}"

         from components.java
      }
   }
}

final YEAR = new Date().format('yyyy')

spotless {
   java {
      licenseHeader """
/* Copyright (c) ${YEAR} StuyPulse Robotics. All rights reserved. */
/* This work is licensed under the terms of the MIT license */
/* found in the root directory of this project. */

"""

      googleJavaFormat('1.13.0').aosp()

      trimTrailingWhitespace()
      endWithNewline()

      importOrder('com.stuypulse.stuylib', 'com.stuypulse', 'edu.wpi')
      removeUnusedImports()
   }
}

// Example to configure HTML report
spotbugsMain {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

spotbugs {
   ignoreFailures = true
   effort = 'max'
}

javadoc {
  destinationDir = file('docs/')
}

build {
   dependsOn javadoc
}

jar {
   from sourceSets.main.allSource
}
