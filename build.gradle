buildscript {
   ext.WPI_VER = "2021.2.2"
}

plugins {
   id "java"

   id "edu.wpi.first.GradleRIO" version "${WPI_VER}"
   id "com.diffplug.spotless" version "5.10.2"
   id "com.github.spotbugs" version "4.7.0"
}

group = 'com.stuypulse'
version = '1.0.0'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

sourceSets {
   main {
      java {
         srcDir "src"
      }
   }
}

configurations {
   jaxDoclet
}

dependencies { 
   implementation wpi.deps.wpilib()
   nativeZip wpi.deps.wpilibJni(wpi.platforms.roborio)
   nativeDesktopZip wpi.deps.wpilibJni(wpi.platforms.desktop)

   implementation wpi.deps.vendor.java()
   nativeZip wpi.deps.vendor.jni(wpi.platforms.roborio)
   nativeDesktopZip wpi.deps.vendor.jni(wpi.platforms.desktop)

   compile "edu.wpi.first.ntcore:ntcore-java:${WPI_VER}"
   compile "edu.wpi.first.wpiutil:wpiutil-java:${WPI_VER}"
   runtime "edu.wpi.first.ntcore:ntcore-jni:${WPI_VER}:windowsx86"
   runtime "edu.wpi.first.ntcore:ntcore-jni:${WPI_VER}:windowsx86-64"
   runtime "edu.wpi.first.ntcore:ntcore-jni:${WPI_VER}:linuxx86-64"
   runtime "edu.wpi.first.ntcore:ntcore-jni:${WPI_VER}:osxx86-64"

   compile group: 'org.knowm.xchart', name: 'xchart', version: '3.2.2'

   testImplementation 'junit:junit:4.12'

   // Enable simulation gui support. Must check the box in vscode to enable support
   // upon debugging
   simulation wpi.deps.sim.gui(wpi.platforms.desktop, false)
}

spotless {
   java {
      licenseHeader """
      /* Copyright (c) 2021 StuyPulse Robotics. All rights reserved. */
      /* This work is licensed under the terms of the MIT license */
      /* found in the root directory of this project. */
      """

      googleJavaFormat().aosp()

      trimTrailingWhitespace()
      endWithNewline()

      importOrder('com.stuypulse.stuylib', 'com.stuypulse', 'edu.wpi')
      removeUnusedImports()
   }
}

// Example to configure HTML report
spotbugsMain {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

spotbugs {
   effort = 'max'
}

javadoc {
  destinationDir = file('docs/')
}

build {
   dependsOn javadoc
}

jar {
   from sourceSets.main.allSource
}
